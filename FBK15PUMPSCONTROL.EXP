
(* @NESTEDCOMMENTS := 'Yes' *)
(* @PATH := '\/K15' *)
(* @OBJECTFLAGS := '0, 8' *)
(* @SYMFILEFLAGS := '2048' *)
FUNCTION_BLOCK fbK15PumpsControl
(*
	Управление насосами К15
	контура нагрева ГВС
*)
VAR CONSTANT
	PumpCheckDelayTime : TIME := T#10s;
END_VAR
VAR_INPUT
	ManualMode: BOOL;						(*Ручной режим?*)
	WorkAllowed: BOOL;					(*Работа разрешена?*)
	EmergencyPumpStopButtonPressed: ARRAY [1..MAX_K15_PUMPS_NUM] OF BOOL; (*Нажат аварийный стоп насоса?*)
	PumpState: ARRAY [1..MAX_K15_PUMPS_NUM] OF BOOL;	(*Работает насос?*)
	PressureDrop : BOOL;					(*Есть разница давлений?*)
END_VAR
VAR_OUTPUT
END_VAR
VAR
	pumpStatusCheckDelay	: ARRAY [1..MAX_K15_PUMPS_NUM] OF TON;
	pumpsTrainingControl : fbK15PumpsTraining;
	workOrderControl: fbK15PumpOrderControl;
END_VAR

VAR_TEMP
	i : USINT;
	pumpWorkOrder : ARRAY [1..MAX_K15_PUMPS_NUM] OF USINT;
	pumpStatusCheck : ARRAY [1..MAX_K15_PUMPS_NUM] OF BOOL;
	break : BOOL := FALSE;
END_VAR
(* @END_DECLARATION := '0' *)
break := FALSE;

FOR i := 1 TO MAX_K15_PUMPS_NUM DO
	pumpStatusCheckDelay[i](IN := PumpState[i], PT := PumpCheckDelayTime);
	pumpStatusCheck[i] := pumpStatusCheckDelay[i].Q;
END_FOR;

fK15PumpsFaultyControl(
	PumpState				:= PumpState,
	PumpStatusCheckDelay	:= pumpStatusCheck,
	PressureDrop			:= PressureDrop,
	EmergencyPumpStopButtonPressed	:= EmergencyPumpStopButtonPressed
);

IF ManualMode THEN (*Ручной режим?*)
(*
	FOR i := 1 TO MAX_K15_PUMPS_NUM DO
		fK15PumpManualControl(
			PumpID	:= i,
			TurnOn	:= TurnOnPumpButtonPressed[i],
			TurnOff	:= TurnOffPumpButtonPressed[i],
			State		:= PumpState[i],
			EmergencyStop := EmergencyPumpStopButtonPressed[i]
		);
	END_FOR;
*)
	(*Контроллер никак не участвует в ручном режиме*)
	FOR i := 1 TO MAX_K15_PUMPS_NUM DO
		fTurnOffK15Pump(i);
	END_FOR;
ELSE
	IF WorkAllowed THEN	(*Работа разрешена?*)
		(*определяем порядок включения насосов*)
		workOrderControl(pumpWorkOrder := pumpWorkOrder);

		FOR i := 1 TO MAX_K15_PUMPS_NUM DO
			(*
				работаем с насосами в порядке их приоритета
				Ищем первый исправный насос и включаем его
			*)
			IF NOT(break) AND NOT(K15PumpFaulty[pumpWorkOrder[i]]) THEN
				fK15PumpAutoControl(
						PumpID := pumpWorkOrder[i],
						EmergencyPumpStopButtonPressed := EmergencyPumpStopButtonPressed[pumpWorkOrder[i]]
				);
				break := TRUE;
			END_IF;
		END_FOR;
			(*периодическая тренировка насосов*)
		pumpsTrainingControl();
	ELSE
		(*
			если работа не разрешена,
			выключаем все насосы
		*)
		FOR i := 1 TO MAX_K15_PUMPS_NUM DO
			fTurnOffK15Pump(i);
		END_FOR;
		RETURN;
	END_IF;
END_IF;
END_FUNCTION_BLOCK
